### italian.foma ###

define V [a | o | u | e | i] ;
define C [b | c | d | f | g | h | j | k | l | m | n | p | q | r | s | t | v | w | x | y | z];
define Cond [ r e i | r e s t i | r e b b e | r e m m o | r e s t e | r e b b e r o ];
# Rules for writing verbs
define VerbSolidK [..] -> [ h ] || [ c ] _ [ a r e ] [ "^" | "$" ] [ i | e ];
define VerbSolidKCond [..] -> [ h ] || [ c ] _ [ a r e ] [ "^" | "$" ] Cond;
define VerbPresenteSg12Pl1 [a r e | e r e | i r e] -> 0 || _ "^" [ o | i | i a m o ];
define VerbPresenteSg3First [ r e "^" e ] -> "^" || [ a ] _;
define VerbPresenteSg3SecondThird [ e r e | i r e ] -> 0 || _ "^" [ e ];
define VerbPresentePl2 [ r e ] -> 0 || [a | e | i ] _ "^" [ t e ];
define VerbPresentePl3First [ r e "^" o ] -> "^" || [ a ] _ [ n o ];
define VerbPresentePl3SecondThird [ e r e | i r e ] -> 0 || _ "^" [ o n o ];

define VerbFuturoSempliceFirst [ a r e ] -> [ e r ] ||  _ "^" [ ò | a i | à | e m o | e t e | a n n o]; 
define VerbFuturoSempliceSecondThird [ e ] -> 0 || [ i r | e r ]  _ "^" [ ò | a i | à | e m o | e t e | a n n o];

#Experimental
define VerbPassatoRemoto1 [ r e ] -> 0 || [ a | e | i ] _ "$" [ i ];
define VerbPassatoRemoto3First [ a r e ] "^" -> "ò" || _ "'";
define VerbPassatoRemoto3Second [ r e ] "^" -> 0 || _ "'";
define VerbPassatoRemoto3Third [ i r e ] "^" -> "ì" || _ "'";
define VerbPassatoRemotoRest [ r e ] -> 0 || [ a | e | i ] _ "^" [ s t i | m m o | r o n o];

define VerbImperfetto [ r e ] -> 0 || [ a | e | i ] _ "^" [ v o | v i | v a ];
define VerbCondizionaleFirst [ a r e ] -> e || _ "^" Cond;
define VerbCondizionaleRest [ r e ] -> 0 || [ e | i ] _ "^" Cond;

define VerbCondizionalePassatoSecond [ e r e ] -> u ||  _ "^" [ t o ];
define VerbCondizionalePassatoRest [ r e ] -> 0 || [ a | i ] _ "^" [ t o | t i ];

define VerbCongiuntivoImperfetto [ r e ] -> 0 || [ a | e | i ] _ "^" [ s s l | s s e | s s i m o | s t e | s s e r o ];
define VerbGerundioFirstSecond [ r e ] -> 0 || [ a | e ] _ "^" [ n d o ];
define VerbGerundioThird [ i r e ] -> e || _ "^" [ n d o ];
define VerbCongiuntivoPresenteSgFirst [ a r e ] "^" [ a ] -> [ i ] || _;
define VerbCongiuntivoPresenteSgRest [ i | e ] [ r e ] -> 0 || _ "^" [ a ];
define VerbCongiuntivoPresentePl12 [ a | i | e ] [ r e ] -> 0 || _ "^" [ i a m o | i a t e];
define VerbCongiuntivoPresentePl3First [ a r e ] "^" [ a ] -> "^" [ i ] || _ [ n o ];
define VerbCongiuntivoPresentePl3Rest [[ i | e ] r e ] -> 0 || _ "^" [ a n o ];

# Rules for writing nouns
define NounMPl [o | e] -> 0 || _ "^" i ;
define NounFPl [a] -> 0 || _ "^" e ;
define NounCGoEndingPl [o] -> h || C [c | g] _ "^" i ;
define NounCGaEndingPl [a] -> h || [c | g] _ "^" e ;
define NounProfessionPl a -> 0 || i s t _ "^" [e | i] ;
define NounGreekPl a -> 0 || [m | t] _ "^" i ;
define NounIoEndingPl [i o] -> 0 || _ "^" i ;
define NounCiaEndingPl [i a] -> 0 || [c | g] _ "^" e ;
define NounUomini [o -> [i n] || u o m _ "^" i] .o.
                  [o -> [i n i] || u o m _ "-"] ;

# Rules for writing adjectives
define AdjFSg [o] -> 0 || _ "^" a ;
define AdjMPl [o | e] -> 0 || _ "^" i ;
define AdjFPl [o] -> 0 || _ "^" e ;
define AdjCGoEndingPl [o] -> h || C [c | g] _ "^" i ;
define AdjCGaEndingPl [o] -> h || [c | g] _ "^" e ;
define AdjProfessionPl a -> 0 || i s t _ "^" [e | i] ;
define AdjCioEndingPl [i o] -> 0 || [c | g] _ "^" i ;
define AdjCiaEndingPl [[i a] -> i || V [c | g] _ "^" e] |
                [[i a] -> 0 || C [c | g] _ "^" e];
define AdjPresenteParticipio [i -> e || _ r e "^" n t [e | i]] .o.
                            [[r e] -> 0 || _ "^" n t [e | i]] ;


#ii is not common in this language. So only i$i will remain.
define DoubleI [ i ] -> 0 || _ "^" i;

#Cleanup: remove morpheme boundaries
define Cleanup [ "^" | "$" | "'" ] -> 0;

read lexc italian.lexc
define Lexicon

define Grammar Lexicon                        .o.
               VerbSolidK                     .o.
               VerbSolidKCond                 .o.
               VerbPresenteSg3SecondThird     .o.
               VerbPresentePl2                .o.
               VerbPresentePl3First           .o.
               VerbPresentePl3SecondThird     .o.
               VerbFuturoSempliceFirst        .o.
               VerbFuturoSempliceSecondThird  .o.
               VerbPresenteSg3First           .o.
               VerbPresenteSg3SecondThird     .o.
               NounUomini                     .o.
               NounProfessionPl               .o.
               NounGreekPl                    .o.
               NounIoEndingPl                 .o.
               NounCiaEndingPl                .o.
               AdjProfessionPl                .o.
               AdjPresenteParticipio          .o.
               AdjCioEndingPl                 .o.
               AdjCiaEndingPl                 .o.
               VerbPassatoRemoto1             .o.
               VerbPassatoRemoto3First        .o.
               VerbPassatoRemoto3Second       .o.
               VerbPassatoRemoto3Third        .o.
               VerbPassatoRemotoRest          .o.
               VerbImperfetto                 .o.
               VerbCondizionaleFirst          .o.
               VerbCondizionaleRest           .o.
               VerbCondizionalePassatoSecond  .o.
               VerbCondizionalePassatoRest    .o.
               VerbCongiuntivoImperfetto      .o.
               VerbGerundioFirstSecond        .o.
               VerbGerundioThird              .o.
               VerbPresenteSg12Pl1            .o.
               NounCGoEndingPl                .o.
               NounCGaEndingPl                .o.
               AdjCGoEndingPl                 .o.
               AdjCGaEndingPl                 .o.
               VerbCongiuntivoPresenteSgRest   .o.
               VerbCongiuntivoPresentePl12     .o.
               VerbCongiuntivoPresentePl3First .o.
               VerbCongiuntivoPresentePl3Rest  .o.
               VerbCongiuntivoPresenteSgFirst  .o.
               NounMPl                        .o.
               NounFPl                        .o.
               AdjFSg                         .o.
               AdjFPl                         .o.
               AdjMPl                         .o.
               VerbPresenteSg3First           .o. #This rule is really stupid. Need to be fixed
               DoubleI                        .o.
               Cleanup;

regex Grammar;

